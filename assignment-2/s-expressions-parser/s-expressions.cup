/* Just parse integers and return them */
import java_cup.runtime.*;
import java.util.List;
import java.util.ArrayList;

parser code {:
    // Connect this parser to a scanner!
    /*
    scanner s;
    Parser(scanner s){ this.s=s; }
    */
:}

/* define how to connect to the scanner! */
// init with {: s.init(); :};
// scan with {: return s.next_token(); :};

terminal Integer   INTEGER;
terminal Double    DOUBLE;
terminal String    STRING;
terminal String    SYMBOL;
terminal OPENING_PAREN, CLOSING_PAREN, QUOTE, ERROR;

non terminal SExpr       atom;
non terminal SExpr       s_expr;
non terminal SExpr       quoted_s_expr;
non terminal SExpr       unquoted_s_expr;
non terminal List<SExpr> expr_list;
non terminal program;


/* The grammar rules */

program ::= s_expr:s {: System.out.println(s.toString()); :}
;

s_expr ::= unquoted_s_expr: e {: RESULT = e; :}
         |  quoted_s_expr: e  {: RESULT = e; :}
;

quoted_s_expr ::= QUOTE s_expr:e {: RESULT = SExpr.SExprQuoted(e); :}
;

unquoted_s_expr ::= OPENING_PAREN expr_list:l CLOSING_PAREN {: RESULT = SExpr.SExprList(l); :}
                  | atom:a {: RESULT = a; :}
;

expr_list ::= expr_list:l s_expr:e {: l.add(l.size(), e); RESULT = l; :}
            |  /* empty list */    {: RESULT = new ArrayList<SExpr>(); :}
;

atom ::= INTEGER:i {: RESULT = SExpr.SExprInt(i);    :}
       | DOUBLE:d  {: RESULT = SExpr.SExprDouble(d); :}
       | STRING:s  {: RESULT = SExpr.SExprString(s); :}
       | SYMBOL:s  {: RESULT = SExpr.SExprSymbol(s); :}
;
